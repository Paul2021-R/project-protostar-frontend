def failureStage = "Unknown" // ê¸°ë³¸ê°’

pipeline {
    agent any

    tools {
        nodejs 'NodeJS-22'
    }
    
    environment {
        GHCR_CREDS_ID = 'ghcr-creds'
        GITHUB_CREDS_ID = 'github-creds'
        TARGET_SSH_CREDS_ID = 'a5-localhost-jenkins'

        TARGET_DEPLOY_SERVER = 'GEEKOM-A5-Server'

        DISCORD_CREDS_ID = 'discord-webhook-url'

        IMAGE_NAME = 'ghcr.io/paul2021-r/project-protostar-frontend'

        TARGET_HOST = credentials('ssh-host-creds')
        TARGET_PORT = credentials('ssh-port-creds')
        TARGET_PROJECT_PATH = '/home/hansol/workspace/project-protostar-frontend'
        TARGET_INFRA_PATH = '/home/hansol/workspace/project-protostar-server-configs/main-server'

        // Frontend Environment Variables
        NEXT_PUBLIC_API_URL = 'https://service-protostar.ddns.net'
    }

    parameters {
        choice(name: 'TARGET_COLOR', choices: ['Auto', 'Blue', 'Green', 'Both'], description: 'Select the color to deploy. Auto follows BUILD_NUMBER odd/even logic.')
        choice(name: 'DEPLOY_ACTION', choices: ['Full Deploy', 'Build & Deploy Only', 'Switch Traffic Only'], description: 'Select the scope of the deployment.')
    }

    stages {
    // code pull
        stage('Checkout') {
            steps {
                script {
                    try {
                        echo "1. Get the latest code from GitHub"
                        git credentialsId: GITHUB_CREDS_ID, url: 'https://github.com/paul2021-r/project-protostar-frontend.git', branch: 'main'
                    } catch (Exception e) {
                        failureStage = "Checkout Stage"
                        throw e
                    }
                }
            }
        }

        stage('Install Dependencies') {
            steps {
                echo "2. Install Dependencies"
                script {
                    try {
                        dir('app') {
                            sh "pnpm install"
                        }
                    } catch (Exception e) {
                        failureStage = "Install Dependencies Stage"
                        throw e
                    }
                }
            }
        }

        stage('Code Style Check') {
            steps {
                echo "3. Check Code Style and Format"
                script {
                    dir('app') {
                        // 1. try ë¸”ë¡ìœ¼ë¡œ ë©”ì¸ ë¡œì§ì„ ê°ìŒ‰ë‹ˆë‹¤.
                        try {
                            def exitCode = sh(script:"pnpm format:check > output.log 2>&1", returnStatus: true)
                            def output = readFile('output.log').trim()

                            if (exitCode != 0) {
                                withCredentials([string(credentialsId: DISCORD_CREDS_ID, variable: 'DISCORD_URL')]) {
                                    
                                    // (ì¶”ê°€ ê°œì„ ) outputì— "ë‚˜ \ê°€ í¬í•¨ë˜ë©´ JSONì´ ê¹¨ì§ˆ ìˆ˜ ìˆìœ¼ë¯€ë¡œ ì´ìŠ¤ì¼€ì´í”„í•©ë‹ˆë‹¤.
                                    def jsonSafeOutput = output.replace('\\', '\\\\').replace('"', '\\"').replace('\n', '\\n')
                                    
                                    def message = "{\"content\": \"âŒ Formatter Check is failed. Check logs ${env.BUILD_URL} : **[${env.JOB_NAME}]** - **#${env.BUILD_NUMBER}**\nerrorLog(${exitCode}): ${jsonSafeOutput}\"}"

                                    sh "curl -X POST -H 'Content-Type: application/json' --data '${message}' ${DISCORD_URL}"
                                }
                                // Jenkins ë¡œê·¸ì—ëŠ” ì›ë³¸ outputì„ ë‚¨ê¹ë‹ˆë‹¤.
                                error "Code Formatting check Failed(${exitCode}) ${output}"    
                            }
                            else {
                                echo "Formatter check is successful."
                            }
                        } catch (Exception e) {
                            failureStage = "Code Style Check Stage"
                            throw e
                        } finally {
                            //    ì´ ë¸”ë¡ì€ ìœ„ì—ì„œ errorê°€ ë°œìƒí•´ë„, ì„±ê³µí•´ë„ "í•­ìƒ" ì‹¤í–‰ë©ë‹ˆë‹¤.
                            echo "Cleaning up output.log..."
                            // (ì¶”ê°€ ê°œì„ ) -f ì˜µì…˜ìœ¼ë¡œ íŒŒì¼ì´ ì—†ì–´ë„ ì—ëŸ¬ë¥¼ ë‚´ì§€ ì•Šë„ë¡ í•©ë‹ˆë‹¤.
                            sh "rm -f output.log"
                        }   
                        
                    }
                }
            }
        }

        stage('Check Lint') {
            steps {
                echo "4. Check lint"
                script {
                    dir('app') {
                        // 1. try ë¸”ë¡ìœ¼ë¡œ ë©”ì¸ ë¡œì§ì„ ê°ìŒ‰ë‹ˆë‹¤.
                        try {
                            def exitCode = sh(script:"pnpm lint > lint-output.log 2>&1", returnStatus: true)
                            def output = readFile('lint-output.log').trim()

                            if (exitCode != 0) {
                                withCredentials([string(credentialsId: DISCORD_CREDS_ID, variable: 'DISCORD_URL')]) {
                                    
                                    // (ì¶”ê°€ ê°œì„ ) outputì— "ë‚˜ \ê°€ í¬í•¨ë˜ë©´ JSONì´ ê¹¨ì§ˆ ìˆ˜ ìˆìœ¼ë¯€ë¡œ ì´ìŠ¤ì¼€ì´í”„í•©ë‹ˆë‹¤.
                                    def jsonSafeOutput = output.replace('\\', '\\\\').replace('"', '\\"').replace('\n', '\\n')
                                    
                                    def message = "{\"content\": \"âŒ Lint Check is failed. Check logs ${env.BUILD_URL} : **[${env.JOB_NAME}]** - **#${env.BUILD_NUMBER}** errorLog(${exitCode}): ${jsonSafeOutput}\"}"

                                    sh "curl -X POST -H 'Content-Type: application/json' --data '${message}' ${DISCORD_URL}"
                                }
                                // Jenkins ë¡œê·¸ì—ëŠ” ì›ë³¸ outputì„ ë‚¨ê¹ë‹ˆë‹¤.
                                error "Code Linting check Failed(${exitCode}) ${output}"    
                            }
                            else {
                                echo "Lint check is successful."
                            }
                        } catch (Exception e) {
                            failureStage = "Lint Check Stage"
                            throw e
                        } finally {
                            // 2. 'finally' ë¸”ë¡ì— ì •ë¦¬ ì½”ë“œë¥¼ ë‘¡ë‹ˆë‹¤.
                            //    ì´ ë¸”ë¡ì€ ìœ„ì—ì„œ errorê°€ ë°œìƒí•´ë„, ì„±ê³µí•´ë„ "í•­ìƒ" ì‹¤í–‰ë©ë‹ˆë‹¤.
                            echo "Cleaning up lint-output.log..."
                            // (ì¶”ê°€ ê°œì„ ) -f ì˜µì…˜ìœ¼ë¡œ íŒŒì¼ì´ ì—†ì–´ë„ ì—ëŸ¬ë¥¼ ë‚´ì§€ ì•Šë„ë¡ í•©ë‹ˆë‹¤.
                            sh "rm -f lint-output.log"
                        }   
                        
                    }
                }
            }
        }

        // build image
        stage('Build Image') {
            when {
                expression { params.DEPLOY_ACTION != 'Switch Traffic Only' }
            }
            steps {
                echo "5. Build New Docker Image"
                script {
                    try {
                        def imageTag = "${IMAGE_NAME}:${env.BUILD_NUMBER}" 
                        sh "docker build --build-arg NEXT_PUBLIC_API_URL=${env.NEXT_PUBLIC_API_URL} -t ${imageTag} ./app" 
                    } catch (Exception e) {
                        failureStage = "Build Stage"
                        throw e
                    }
                }
            }
        }

        // push image
        stage('Push to GHCR') {
            when {
                expression { params.DEPLOY_ACTION != 'Switch Traffic Only' }
            }
            steps {
                echo "6. Push the Docker Image to GHCR"
                script {
                    try {
                        def imageTag = "${IMAGE_NAME}:${env.BUILD_NUMBER}"
                        withCredentials([usernamePassword(credentialsId: GHCR_CREDS_ID, usernameVariable: 'USER', passwordVariable: 'TOKEN')]) {
                            sh "echo ${TOKEN} | docker login ghcr.io -u ${USER} --password-stdin"
                            sh "docker push ${imageTag}"
                        }
                    } catch (Exception e) {
                        failureStage = "Push to GHCR Stage"
                        throw e
                    }
                }
            }
        }

        // deploy to server
        stage('Deploy to Server') {
            steps {
                echo "7. Deploy the new image to the ${TARGET_DEPLOY_SERVER} server"
                script {
                    try {
                        // 1. Determine Target Services
                        def deployServices = []
                        if (params.TARGET_COLOR == 'Both') {
                             deployServices = ['nextjs-blue', 'nextjs-green']
                        } else if (params.TARGET_COLOR == "Auto") {
                             deployServices = [(env.BUILD_NUMBER.toInteger() % 2 == 0) ? "nextjs-green" : "nextjs-blue"]
                        } else if (params.TARGET_COLOR == "Blue") {
                             deployServices = ["nextjs-blue"]
                        } else {
                             deployServices = ["nextjs-green"]
                        }

                        echo "Target Services via ${params.TARGET_COLOR}: ${deployServices}"

                        def imageTag = "${IMAGE_NAME}:${env.BUILD_NUMBER}"
                        def newTag = "${env.BUILD_NUMBER}"
                        def earlyTag = "${Math.max(1, env.BUILD_NUMBER.toInteger() - 1)}"
                        
                        withCredentials([usernamePassword(credentialsId: GHCR_CREDS_ID, usernameVariable: 'GHCR_USER', passwordVariable: 'GHCR_TOKEN')]) {
                            sshagent(credentials: [TARGET_SSH_CREDS_ID]) {
                                // 2. Deployment Script Construction
                                def commands = []

                                // Common Setup
                                commands << "set -e"
                                
                                // Action: Build & Deploy (or Full)
                                if (params.DEPLOY_ACTION != 'Switch Traffic Only') {
                                    commands << "echo '--- Logging in to GHCR on target server ---'"
                                    commands << "echo '${GHCR_TOKEN}' | docker login ghcr.io -u ${GHCR_USER} --password-stdin"

                                    commands << "echo '--- (1/4) Pull the new image : ${imageTag} ---'"
                                    commands << "docker pull ${imageTag}"

                                    commands << "echo '--- (2/4) Replace Server(s) to new  ---'"
                                    commands << "cd ${TARGET_PROJECT_PATH}"
                                    
                                    commands << "echo '--- Refresh main repository ---'"
                                    commands << "git pull origin main" 
                                    commands << "git checkout main"

                                    // Loop through services
                                    deployServices.each { service ->
                                        def tagEnvVar = (service == "nextjs-green") ? "TAG_GREEN" : "TAG_BLUE"
                                        commands << "echo '--- Exporting new tag for ${service}: ${tagEnvVar}=${newTag} ---'"
                                        commands << "export ${tagEnvVar}='${newTag}'"
                                    }
                                    
                                    // If 'Single' case, we formerly exported the opposite tag too.
                                    if (params.TARGET_COLOR != 'Both') {
                                        def activeService = deployServices[0]
                                        def tagOppositeEnvVar = (activeService == "nextjs-green") ? "TAG_BLUE" : "TAG_GREEN"
                                        commands << "export ${tagOppositeEnvVar}='${earlyTag}'"
                                    }

                                    deployServices.each { service ->
                                        commands << "docker compose --env-file init.env up -d --no-deps --force-recreate ${service}"
                                    }
                                    
                                    commands << "echo '--- Logging out from GHCR on target server ---'"
                                    commands << "docker logout ghcr.io"
                                }

                                // Action: Switch Traffic (or Full)
                                if (params.DEPLOY_ACTION != 'Build & Deploy Only') {
                                    commands << "echo '--- (3/4) Switch Nginx Configuration to change traffic ---'"
                                    
                                    // Only switch upstream if we are targeting a specific single color
                                    if (params.TARGET_COLOR != 'Both') {
                                        def targetService = deployServices[0]
                                        commands << "cd ${TARGET_INFRA_PATH}"
                                        commands << "sed -i 's|server main-nextjs-.*:3000;|server main-${targetService}:3000;|' ./nginx/nginx_upstream_nextjs.conf"   
                                    } else {
                                        commands << "echo 'Skipping Nginx Upstream Switch because TARGET_COLOR is Both (Preserving current active upstream)'"
                                    }

                                    commands << "echo '--- (4/4) Reload Nginx Configuration ---'"
                                    commands << "docker exec main-protostar-nginx nginx -s reload"
                                }

                                commands << "echo '--- All Deploy Proccess is done ---'"
                                
                                def deployScript = commands.join('\n')
                                sh "ssh -o StrictHostKeyChecking=no -o ConnectTimeout=10 -o ServerAliveInterval=60 -o ServerAliveCountMax=3 -p ${TARGET_PORT} ${TARGET_HOST} \"${deployScript}\""
                            }
                        }
                    } catch (Exception e) {
                        failureStage = "Deploy Stage"
                        throw e
                    }
                }
            }
        }
    }
   
    post {
        success {
            echo "Deployment succeeded. Now send a discord notification."
            script {
                withCredentials([string(credentialsId: DISCORD_CREDS_ID, variable: 'DISCORD_URL')]) {
                    def message = "{\"content\": \"ğŸš€ Build Process is successfully finished : **[${env.JOB_NAME}]** - **#${env.BUILD_NUMBER}**\"}"

                    sh "curl -X POST -H 'Content-Type: application/json' --data '${message}' ${DISCORD_URL}"
                }

            }

        }
        failure {
            echo "Deployment failed! Now send a discord notification."

            script {
                withCredentials([string(credentialsId: DISCORD_CREDS_ID, variable: 'DISCORD_URL')]) {
                    def message = "{\"content\": \"ğŸ”¥ Build Process is failed at ${failureStage}. Check Build logs ${env.BUILD_URL} : **[${env.JOB_NAME}]** - **#${env.BUILD_NUMBER}**\"}"

                    sh "curl -X POST -H 'Content-Type: application/json' --data '${message}' ${DISCORD_URL}"
                }    
            }
            
        }
        always {
            echo "Logout from GHCR"
            sh "docker logout ghcr.io"
        }
    }
}